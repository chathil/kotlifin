plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'com.squareup.wire'
}

android {
    namespace 'com.chathil.kotlifin'
    compileSdk 33

    defaultConfig {
        applicationId "com.chathil.kotlifin"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.7'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    wire {
        kotlin {
            sourcePath {
                srcDir 'src/main/protos'
            }
            android = true
        }
    }
}

//
// ref: https://github.com/square/wire/issues/2335
//

def wireTaskSpec = new Spec<Task>() {
    @Override
    boolean isSatisfiedBy(Task task) {
        return task.class.name.startsWith("com.squareup.wire.gradle.WireTask")
    }
}

def kspTaskSpec = new Spec<Task>() {
    @Override
    boolean isSatisfiedBy(Task task) {
        return task.class.name.startsWith("com.google.devtools.ksp.gradle.KspTaskJvm")
    }
}

def wireTasks = tasks.matching(wireTaskSpec)

tasks.matching(kspTaskSpec).configureEach { task ->
    task.dependsOn(wireTasks)
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$runtime_compose_version"
    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation "androidx.compose.material:material-icons-extended:$compose_material_icon_extended"
    implementation "androidx.navigation:navigation-compose:$nav_version"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "io.coil-kt:coil-compose:$coil_version"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:$hilt_compose_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation "androidx.paging:paging-runtime:3.2.0"
    implementation "androidx.paging:paging-compose:1.0.0-alpha20"

    implementation "org.jellyfin.sdk:jellyfin-core:1.4.2"

    implementation "androidx.datastore:datastore:1.0.0"

    implementation "com.jakewharton.timber:timber:5.0.1"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'

    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}

kapt {
    correctErrorTypes true
}